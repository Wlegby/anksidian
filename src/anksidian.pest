file = { SOI ~ (cloze_lines | heading | tag | code | math | link | ANY)* ~ EOI }

heading = { "#"+ ~ " " ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

tag = { "#" ~ !("#" | " " | NEWLINE) ~ (!(NEWLINE | " ") ~ ANY)+ ~ NEWLINE }

cloze = { "==" ~ (!"==" ~ (math | link | character))+ ~ "==" }

/// Lines containing at least one cloze.
// also matches pure clozes
cloze_lines          = { not_cloze_or_newline* ~ cloze ~ (not_cloze_or_newline | cloze)* ~ (NEWLINE ~ note_id_comment)? }
not_cloze_or_newline = { !(cloze | "\n") ~ (code | math | link | character) }
note_id_comment      = { "<!--" ~ ascii_digits ~ "-->" ~ NEWLINE? }
ascii_digits         = { ASCII_BIN_DIGIT+ }

code           = { multiline_code | inline_code }
multiline_code = { "```" ~ (!"```" ~ ANY)* ~ "```" }
inline_code    = { "`" ~ (!"`" ~ ANY)* ~ "`" }

math         = { inline_math | display_math }
inline_math  = { "$" ~ (!"$" ~ ANY)+ ~ "$" }
display_math = { "$$" ~ (!"$$" ~ ANY)+ ~ "$$" }

link        = { "[[" ~ (!("]]" | NEWLINE | "|") ~ ANY)+ ~ link_rename? ~ "]]" }
link_rename = { "|" ~ (!("]]" | NEWLINE) ~ ANY)+ }

/// Same as ANY, but shows up in the list of children spans
character = { ANY }
