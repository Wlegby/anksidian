file = { SOI ~ (cloze_lines | heading | tag | code | math | link | ANY)* ~ EOI }

heading = { "#"+ ~ " " ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

tag = { "#" ~ !("#" | " " | NEWLINE) ~ (!(NEWLINE | " ") ~ ANY)+ ~ NEWLINE }

cloze = { "==" ~ (!"==" ~ ANY)+ ~ "==" }
// also matches pure clozes
/// Lines containing at least one cloze.
cloze_lines          = { not_cloze_or_newline* ~ cloze ~ (not_cloze_or_newline | cloze)* ~ (NEWLINE ~ note_id_comment)? }
not_cloze_or_newline = { !(cloze | "\n") ~ (code | math | link | ANY) }
note_id_comment      = { "<!--" ~ ASCII_DIGIT+ ~ "-->" }

code           = { multiline_code | inline_code }
multiline_code = { "```" ~ (!"```" ~ ANY)* ~ "```" }
inline_code    = { "`" ~ (!"`" ~ ANY)* ~ "`" }

math         = { inline_math | display_math }
inline_math  = { "$" ~ (!"$" ~ ANY)+ ~ "$" }
display_math = { "$$" ~ (!"$$" ~ ANY)+ ~ "$$" }

link        = { "[[" ~ (!("]]" | NEWLINE | "|") ~ ANY)+ ~ link_rename? ~ "]]" }
link_rename = { "|" ~ (!("]]" | NEWLINE) ~ ANY)+ }
